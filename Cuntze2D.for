! WRITTEN BY VINEETH RAVI
! FOLLOWING IS A UMAT CODE FOR LAMINA
! VARIABLES FROM THE LETTER I TO N ARE CONSIDEREDED TO BE INTEGERS
! AND VARIABLES STARTING WITH OTHER LETTERS ARE DOUBLE PRECISION
! SO PLEASE BE CAREFUL IN NAMING THE VARIABLES
! EXAMPLES OF FEW DEFAULT INTEGERS : NTENS, NDI, NSHR
! EXAMPLES OF FEW DEFAULT REAL NUMBERS : DDSDDE, STRESS, STRAN
! THE KEYWORD *Transverse Shear Stiffness NEEDS TO ENTERED BEFORE CARRYING OUT THE SIMULATIONS
! MORE DETAILS CAN BE FOUND IN THE ABAQUS MANUAL



      SUBROUTINE UMAT(STRESS,STATEV,DDSDDE,SSE,SPD,SCD,
     1 RPL,DDSDDT,DRPLDE,DRPLDT,
     2 STRAN,DSTRAN,TIME,DTIME,TEMP,DTEMP,PREDEF,DPRED,CMNAME,
     3 NDI,NSHR,NTENS,NSTATV,PROPS,NPROPS,COORDS,DROT,PNEWDT,
     4 CELENT,DFGRD0,DFGRD1,NOEL,NPT,LAYER,KSPT,JSTEP,KINC)

      INCLUDE 'ABA_PARAM.INC'

      CHARACTER*80 CMNAME
      DIMENSION STRESS(NTENS),STATEV(NSTATV),
     1 DDSDDE(NTENS,NTENS),DDSDDT(NTENS),DRPLDE(NTENS),
     2 STRAN(NTENS),DSTRAN(NTENS),TIME(2),PREDEF(1),DPRED(1),
     3 PROPS(NPROPS),COORDS(3),DROT(3,3),DFGRD0(3,3),DFGRD1(3,3),
     4 JSTEP(4)
	 
      PARAMETER (ZERO = 0.0D0, ONE = 1.0D0, TWO = 2.0D0)
	 
      E11 = PROPS(1)    ! ELASTIC MODULUS IN THE LONGITUDINAL 1 DIRECTION
      E22 = PROPS(2)    ! ELASTIC MODULUS IN THE TRANSVERSE 2 DIRECTION
      PR12 = PROPS(3)   ! MAJOR POISSONS RATIO IN 1-2 PLANE
      G12 = PROPS(4)    ! SHEAR MODULUS IN 1-2 PLANE
      XT = PROPS(5)     ! LONGITUDINAL STRENGTH IN THE 1 DIRECTION
      XC = PROPS(6)     ! COMPRESSIVE STRENGTH IN THE 1 DIRECTION
      YT = PROPS(7)     ! LONGITUDINAL STRENGTH IN THE 2 DIRECTION
      YC = PROPS(8)     ! COMPRESSIVE STRENGTH IN THE 2 DIRECTION
      S = PROPS(9)      ! SHEAR STRENGTH IN THE 1-2 PLANE
      F21 = PROPS(10)   ! FRICTION COEFFICIENT
      EX = PROPS(11)    ! MODE INTERACTION COEFFICIENT

      PR21 = (E22*PR12)/E11     ! CALCULAION OF MINOR POISSONS RATIO  
		   
      DO I=1,NTENS  ! INITIALIZATION OF JACOBIAN MATRIX
          DO J=1,NTENS
              DDSDDE(I,J) = ZERO
          ENDDO
      ENDDO
	   
      DDSDDE(1,1) = E11/(1-PR12*PR21)
      DDSDDE(1,2) = PR12*E22/(1-PR12*PR21)
      DDSDDE(2,1) = PR12*E22/(1-PR12*PR21)
      DDSDDE(2,2) = E22/(1-PR12*PR21)
      DDSDDE(3,3) = G12
      
      DO I=1, NTENS
          DO J=1, NTENS
              STRESS(I) = STRESS(I) + DDSDDE(I, J)*DSTRAN(J) ! UPDATING STRESS TENSOR
          END DO
      END DO
      
      STATEV = ZERO
      S11=STRESS(1)     ! NORMAL STRESS ALONG 1 DIRECTION
      S22=STRESS(2)     ! NORMAL STRESS ALONG 2 DIRECTION 
      S12=STRESS(3)     ! SHEAR STRESS IN 1-2 PLANE 
	  
      STRAN11 = STRAN(1)+DSTRAN(1)
      
      IF ( S11 .GT. 0.0 ) THEN
          STATEV(1) = (STRAN11*E11)/XT      ! NORMAL FAILURE : FF1
      ELSE
          STATEV(2) = -STRAN11*E11/XC       ! SHEAR FAILURE : FF2
      END IF
      
      IF ( S22 .GT. 0.0 ) THEN
          STATEV(3) = S22/YT                ! NORMAL FAILURE : IFF1
      ELSE
          STATEV(4) = -S22/YC               ! SHEAR FAILURE : IFF2
      END IF
      
      STATEV(5) = ABS(S12)/(S - F21*S22)    ! SHEAR FAILURE : IFF3
      
      ! RESULTANT FAILURE
      STATEV(6) = ( STATEV(1)**EX + STATEV(2)**EX + STATEV(3)**EX + STATEV(4)**EX + STATEV(5)**EX )**(1./EX)
      
      RETURN
      END